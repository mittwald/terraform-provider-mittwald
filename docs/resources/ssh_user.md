---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "mittwald_ssh_user Resource - terraform-provider-mittwald"
subcategory: ""
description: |-
  This resource manages an SSH user for a project.
---

# mittwald_ssh_user (Resource)

This resource manages an SSH user for a project. SSH users can authenticate via password or SSH public keys and provide shell access to the project's file system.

## Example Usage

### Basic Example with Public Key

```terraform
resource "mittwald_ssh_user" "deploy" {
  project_id  = mittwald_project.example.id
  description = "Deployment SSH user"

  public_keys = [
    {
      key     = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIExample... deploy@example.com"
      comment = "deploy@example.com"
    }
  ]
}
```

### Example with Password Authentication

```terraform
resource "mittwald_ssh_user" "admin" {
  project_id  = mittwald_project.example.id
  description = "Admin SSH user"
  password    = var.ssh_admin_password
}
```

### Example with Expiration Date

```terraform
resource "mittwald_ssh_user" "temporary" {
  project_id  = mittwald_project.example.id
  description = "Temporary access for contractor"
  expires_at  = "2024-12-31T23:59:59Z"

  public_keys = [
    {
      key     = "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABgQExample..."
      comment = "contractor@company.com"
    }
  ]
}
```

### Integration with Bitbucket Pipelines

This example shows how to set up automated deployments from Bitbucket Pipelines to a mittwald project. It uses the `tls_private_key` resource to generate an SSH key pair that is then configured on both sides.

```terraform
terraform {
  required_providers {
    mittwald = {
      source = "mittwald/mittwald"
    }
    bitbucket = {
      source = "DrFaust92/bitbucket"
    }
    tls = {
      source = "hashicorp/tls"
    }
  }
}

variable "bitbucket_workspace" {
  description = "Bitbucket workspace name"
  type        = string
}

variable "bitbucket_repository" {
  description = "Bitbucket repository slug"
  type        = string
}

variable "mittwald_ssh_hostname" {
  description = "SSH hostname for the mittwald project (found in mStudio, e.g., ssh.xxx.mittwald.net)"
  type        = string
}

variable "mittwald_ssh_host_key_type" {
  description = "SSH host key type (e.g., ssh-ed25519)"
  type        = string
  default     = "ssh-ed25519"
}

variable "mittwald_ssh_host_key" {
  description = "SSH host public key (obtain via: ssh-keyscan -t ed25519 <hostname>)"
  type        = string
}

# Generate an SSH key pair for deployment
resource "tls_private_key" "deploy" {
  algorithm = "ED25519"
}

# Create SSH user on mittwald with the public key
resource "mittwald_ssh_user" "bitbucket_deploy" {
  project_id  = mittwald_project.example.id
  description = "Bitbucket Pipeline Deployment"

  public_keys = [
    {
      key     = tls_private_key.deploy.public_key_openssh
      comment = "bitbucket-pipeline"
    }
  ]
}

# Configure the SSH key in Bitbucket
resource "bitbucket_pipeline_ssh_key" "deploy" {
  workspace   = var.bitbucket_workspace
  repository  = var.bitbucket_repository
  public_key  = tls_private_key.deploy.public_key_openssh
  private_key = tls_private_key.deploy.private_key_openssh
}

# Add mittwald SSH server as known host in Bitbucket
resource "bitbucket_pipeline_ssh_known_host" "mittwald" {
  workspace  = var.bitbucket_workspace
  repository = var.bitbucket_repository
  hostname   = "[${var.mittwald_ssh_hostname}]:22"

  public_key {
    key_type = var.mittwald_ssh_host_key_type
    key      = var.mittwald_ssh_host_key
  }
}

# Output the SSH connection details for use in bitbucket-pipelines.yml
output "ssh_username" {
  description = "The SSH username for connecting to the mittwald project"
  value       = mittwald_ssh_user.bitbucket_deploy.username
}

output "ssh_host" {
  description = "The SSH hostname for the mittwald project"
  value       = var.mittwald_ssh_hostname
}
```

#### Finding Your SSH Hostname

Your mittwald SSH hostname can be found in the mStudio under your project's SFTP/SSH settings. It typically follows the format `ssh.<cluster-id>.mittwald.net`.

#### Obtaining the SSH Host Key

To configure `bitbucket_pipeline_ssh_known_host`, you need the SSH host key of the mittwald server. You can obtain it using `ssh-keyscan`:

```bash
# Replace with your actual mittwald SSH hostname
ssh-keyscan -t ed25519 ssh.xxx.mittwald.net
```

This will output something like:
```
ssh.xxx.mittwald.net ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAA...
```

Extract the key type (`ssh-ed25519`) and the key itself for use in your Terraform variables.

#### Example bitbucket-pipelines.yml

```yaml
pipelines:
  branches:
    main:
      - step:
          name: Deploy to mittwald
          script:
            # SSH_USERNAME comes from Terraform output
            - rsync -avz --delete ./dist/ $SSH_USERNAME@$SSH_HOST:/html/
```

#### Security Considerations

When using `tls_private_key`:

1. **State Security**: The private key will be stored in your Terraform state. Ensure your state backend is encrypted (e.g., S3 with encryption, Terraform Cloud, etc.).

2. **Key Rotation**: Consider implementing key rotation by using `terraform taint tls_private_key.deploy` periodically.

3. **Alternative**: If you prefer not to store private keys in Terraform state, generate the key pair externally and pass them as sensitive variables:

```terraform
variable "deploy_private_key" {
  type      = string
  sensitive = true
}

variable "deploy_public_key" {
  type = string
}

resource "mittwald_ssh_user" "deploy" {
  project_id  = mittwald_project.example.id
  description = "Deployment SSH user"

  public_keys = [
    {
      key     = var.deploy_public_key
      comment = "deployment-key"
    }
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `description` (String) A description for the SSH user
- `project_id` (String) The ID of the project the SSH user belongs to

### Optional

- `active` (Boolean) Whether the SSH user is active. Defaults to `true`.
- `expires_at` (String) The expiration date of the SSH user in RFC3339 format (e.g., `2024-12-31T23:59:59Z`). If not set, the user does not expire.
- `password` (String, Sensitive) Password for SSH authentication. Either `password` or `public_keys` must be provided. Maximum 72 characters. This is a write-only attribute and will not be read back from the API.
- `public_keys` (Attributes List) List of SSH public keys for authentication. Either `public_keys` or `password` must be provided. (see [below for nested schema](#nestedatt--public_keys))

### Read-Only

- `created_at` (String) The creation timestamp of the SSH user in RFC3339 format
- `id` (String) The generated SSH user ID
- `username` (String) The generated username for SSH authentication. This is automatically generated by the API and cannot be changed.

<a id="nestedatt--public_keys"></a>
### Nested Schema for `public_keys`

Required:

- `comment` (String) A comment/label for the key (e.g., email address or identifier)
- `key` (String) The SSH public key (e.g., `ssh-rsa AAAA... user@host`)

## Import

Import is supported using the SSH user ID:

```shell
terraform import mittwald_ssh_user.example <ssh-user-id>
```
